* Package Manager
** Proxy
Set http(s) proxy for Emacs. /You might not need it, or need to change it./
#+begin_src emacs-lisp
(setq url-proxy-services '(("no_proxy" . "127.0.0.1")
                          ("http" . "127.0.0.1:7890")
                          ("https" . "127.0.0.1:7890")))
#+end_src

** Package
Complie external packages for [[https://www.emacswiki.org/emacs/GccEmacs][GccEmacs]].
#+begin_src emacs-lisp
(setq package-native-compile t)
#+end_src

Configure package.el to include MELPA.
#+begin_src emacs-lisp
(require 'package)
(add-to-list 'package-archives '("gnu" . "https://elpa.gnu.org/packages/") t)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t)
(add-to-list 'package-archives '("org" . "http://orgmode.org/elpa/") t)
(package-initialize)
#+end_src

** Use Package
[[https://github.com/jwiegley/use-package][Use Package]] allows you to isolate package configuration in your .emacs file in a way that is both performance-oriented and, well, tidy.

Always ensure the package is installed.
Ensure that use-package is installed. If use-package isn't already installed, it's extremely likely that this is a fresh installation! So we'll want to update the package repository and install use-package before loading the literate configuration.
#+begin_src emacs-lisp
(when (not (package-installed-p 'use-package))
  (package-refresh-contents)
  (package-install 'use-package))
#+end_src

The ~:ensure~ keyword causes the package(s) to be installed automatically if not already present on your system, it won't affect local packages. Enable ~:ensure t~ to be global for all packages.
#+begin_src emacs-lisp
(require 'use-package-ensure)
(setq use-package-always-ensure t)
#+end_src

Always compile packages, and use the newest version available.
#+begin_src emacs-lisp
(use-package auto-compile
  :config (auto-compile-on-load-mode))

(setq load-prefer-newer t)
#+end_src

Keep the packages up to date.
#+begin_src emacs-lisp
(use-package auto-package-update
  :config
  (setq auto-package-update-delete-old-versions t)
  (setq auto-package-update-hide-results t)
  (auto-package-update-maybe))
#+end_src

** Straight
[[https://github.com/raxod502/straight.el][straight]]: next-generation, purely functional package manager for the Emacs hacker.

* User Interface
** Title Bar
# FIX: Doesn't show folder path in dired.
# FIX: Doesn't show file icon when file is in iCloud.
Show icon and full path in title bar.
#+begin_src emacs-lisp
(setq frame-title-format
    '(:eval
       (if buffer-file-name
           (abbreviate-file-name buffer-file-name)
           "%b")))
#+end_src

Make title bar transparent.
#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(ns-transparent-titlebar . t))
(add-to-list 'default-frame-alist '(ns-appearance . light))
#+end_src

** Tool Bar
Disable tool bar.
#+begin_src emacs-lisp
(tool-bar-mode 0)
#+end_src

** Tab Bar
https://www.emacswiki.org/emacs/TabBarMode
https://github.com/manateelazycat/awesome-tab
https://github.com/ema2159/centaur-tabs

** Scroll Bar
Disable scroll bar.
#+begin_src emacs-lisp
(scroll-bar-mode 0)
#+end_src

** Side Bar
https://github.com/Alexander-Miller/treemacs

** Gutter
https://github.com/emacsorphanage/git-gutter

** Fringe
Disable fringe.
#+begin_src emacs-lisp
(set-fringe-mode 0)
#+end_src

** Line Number
Default line number width with 3 characters.
#+begin_src emacs-lisp
(setq-default display-line-numbers-width 3)
#+end_src

Display relative line numbers on text mode and code mode.
#+begin_src emacs-lisp
(setq-default display-line-numbers-type 'relative
              display-line-numbers-current-absolute t)
(add-hook 'text-mode-hook #'display-line-numbers-mode)
(add-hook 'prog-mode-hook #'display-line-numbers-mode)
#+end_src

** Color Theme
Use default color theme.

** Mode Line
# Simple Modeline
#  Row: Column, Scroll Position, Git
#+begin_src emacs-lisp
(use-package doom-modeline
  :init (doom-modeline-mode 1))
(setq doom-modeline-buffer-encoding nil)
(setq doom-modeline-indent-info t)
(setq column-number-mode t)
(dolist (modeline-face '(
      doom-modeline-buffer-path
      doom-modeline-buffer-file
      doom-modeline-project-dir
      doom-modeline-buffer-modified
      doom-modeline-buffer-major-mode
      doom-modeline-info))
  (set-face-attribute modeline-face nil :weight 'normal))
#+end_src

Hide file icon and file name in mode line.
#+begin_src emacs-lisp
(doom-modeline-def-segment buffer-info
  "buffer info segment."
  "")
(doom-modeline-def-segment buffer-info-simple
  "simple buffer info segment."
  "")
#+end_src

Count column number from 1 instead of 0.
#+begin_src emacs-lisp
(setq column-number-indicator-zero-based nil)
#+end_src

* Window Management
** Frame
# TODO: Make the buffers independent in each frame.
# 21. System-wide popup Emacs windows for quick edits
# 11. snails? ivy?
*** Keybindings
=Ctrl + Command + F=: Set window full screen.
#+begin_src emacs-lisp
(global-set-key (kbd "C-s-f") 'toggle-frame-fullscreen)
#+end_src

=Command + N=: Create new window, default with org-mode and evil-mode
#+begin_src emacs-lisp
(global-set-key (kbd "s-n") 'new-empty-frame)

(defun new-empty-frame ()
  "Create a new frame with a new empty buffer. With org-mode and evil-mode enabled."
  (interactive)
  (let ((buffer (generate-new-buffer "untitled")))
    (set-buffer buffer)
    (org-mode)
    (evil-mode 1)
    (display-buffer buffer '(display-buffer-pop-up-frame . nil))))
#+end_src

=Shift + Command + W=: Close current window.
#+begin_src emacs-lisp
(global-set-key (kbd "s-W") 'delete-frame)
#+end_src

*** Behavior
Associate [[https://github.com/willbchang/alfred-open-in-editor][alfred-open-in-editor]] to open folder in a new frame by ~emacsclient~.
#+begin_src emacs-lisp
(server-start)
#+end_src

** Buffer
*** Keybindings
| Keybindings | Features              |
|-------------+-----------------------|
| ~Command + W~ | Close Current Buffer  |
| ~Command + [~ | Go to previous Buffer |
| ~Command + ]~ | Go to next Buffer     |
| ~Command + T~ | Create New Buffer     |
| ~Command + S~ | Save Buffer           |
| ~Command + R~ | Revert Buffer         |
| ~Command + '~ | Move to next Buffer   |
| ~Command + ,~ | Open Preferences      |

Unset =Command + K== for kill buffer.
#+begin_src emacs-lisp
(global-unset-key (kbd "s-k"))
#+end_src

#+begin_src emacs-lisp
(global-set-key (kbd "s-w") 'kill-this-buffer)
(global-set-key (kbd "s-[") 'previous-buffer)
(global-set-key (kbd "s-]") 'next-buffer)
(global-set-key (kbd "s-t") 'new-empty-buffer)
(global-set-key (kbd "s-s") 'save-buffer)
(global-set-key (kbd "s-r") 'revert-buffer)
(global-set-key (kbd "s-'") 'next-window-any-frame)
(global-set-key (kbd "s-,") 'customize)
#+end_src

# http://ergoemacs.org/emacs/emacs_new_empty_buffer.html
#+begin_src emacs-lisp
(defun new-empty-buffer ()
  "Create a new empty buffer.
New buffer will be named ‚Äúuntitled‚Äù or ‚Äúuntitled<2>‚Äù, ‚Äúuntitled<3>‚Äù, etc."
  (interactive)
  (let (($buffer (generate-new-buffer "untitled")))
    (switch-to-buffer $buffer)
    (funcall initial-major-mode)
    (setq buffer-offer-save t)
    $buffer))
#+end_src

*** Behaviors
Save files automatically.
#+begin_src emacs-lisp
(auto-save-visited-mode 1)
#+end_src

Ensure files end with newline.
#+begin_src emacs-lisp
(setq require-final-newline t)
#+end_src

Revert (update) buffers automatically when underlying files are changed externally.
#+begin_src emacs-lisp
(global-auto-revert-mode t)
#+end_src

Disable startup screen.
#+begin_src emacs-lisp
(setq inhibit-startup-screen t)
#+end_src

Empty scratch file on init.
#+begin_src emacs-lisp
(setq initial-scratch-message nil)
#+end_src

Set initial buffer mode to org-mode.
#+begin_src emacs-lisp
(setq-default initial-major-mode 'org-mode)
#+end_src

Save cursor position for each file.
#+begin_src emacs-lisp
(save-place-mode t)
#+end_src

Save/Restore opened files and windows configuration.
#+begin_src emacs-lisp
(desktop-save-mode 1)
#+end_src

Cancel partially typed or accidental command.
#+begin_src emacs-lisp
(define-key key-translation-map (kbd "ESC") (kbd "C-g"))
#+end_src

Ask =y= or =n= instead of =yes= or =no=. Use ~return~ to act ~y~.
#+begin_src emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
(define-key y-or-n-p-map (kbd "RET") 'act)
#+end_src

Disable the ring bell when scroll beyond the document.
#+begin_src emacs-lisp
(setq ring-bell-function 'ignore)
#+end_src

When you double-click on a file in the Mac Finder open it as a buffer in the existing Emacs frame, rather than creating a new frame just for that file.
#+begin_src emacs-lisp
(setq ns-pop-up-frames nil)
#+end_src

* Word Processing
# Diff text
# 12. company for elisp, especially for completion emacs functions/variables
# 22. Edit comment or string/docstring or code block inside them in separate buffer with your favorite mode https://github.com/twlz0ne/separedit.el
** Basic Features
*** Displaying Text
**** Font
Use MacOS default font SF Mono.
#+begin_src emacs-lisp
(set-face-attribute 'default nil
                    :font "SF Mono-18")
#+end_src

Use MacOS default emoji font üòÉ.
#+begin_src emacs-lisp
(set-fontset-font "fontset-default" 'unicode "Apple Color Emoji" nil 'prepend)
#+end_src

#+begin_src emacs-lisp
(global-set-key (kbd "s-0") 'reset-font-size)
(global-set-key (kbd "s-=") 'text-scale-increase)
(global-set-key (kbd "s--") 'text-scale-decrease)

(defun reset-font-size ()
  (interactive)
  (text-scale-set 0))
#+end_src

**** Behaviors
Highlight urls and make them clickable.
#+begin_src emacs-lisp
(goto-address-mode 1)
#+end_src

Highlight paired brackets, includes (), [], {} and so on...
#+begin_src emacs-lisp
(show-paren-mode 1)
(require 'paren)
(set-face-background 'show-paren-match (face-background 'default))
(set-face-foreground 'show-paren-match "#e2416c")
(set-face-attribute 'show-paren-match nil :weight 'extra-bold)
#+end_src

Enable word wrap globally.
#+begin_src emacs-lisp
(global-visual-line-mode 1)
#+end_src

*** Moving Cursor
Make =Command + ArrowKey= behaves like MacOS app.
- =Command + ‚Üë=: move to the top of the file.
- =Command + ‚Üì=: move to the bottom of the file.
- =Command + ‚Üê=: move to the beginning of the line.
- =Command + ‚Üí=: move to the end of the line.
#+begin_src emacs-lisp
(global-set-key (kbd "s-<up>") 'beginning-of-buffer)
(global-set-key (kbd "s-<down>") 'end-of-buffer)
(global-set-key (kbd "s-<right>") 'move-end-of-line)
(global-set-key (kbd "s-<left>") 'move-beginning-of-line)
#+end_src

*** Searching Text

*** Selecting Text

*** Editing Text
**** Keybindings
- =Command + C=: Copy text.
- =Command + X=: Cut text.
- =Command + V=: Paste text.
#+begin_src emacs-lisp
(global-set-key (kbd "s-c") 'kill-ring-save)
(global-set-key (kbd "s-x") 'kill-region)
(global-set-key (kbd "s-v") 'yank)
#+end_src

=Command + Backspace=: Delete current line from cursor to the beginning
#+begin_src emacs-lisp
(global-set-key (kbd "s-<backspace>") 'backward-kill-line)

(defun backward-kill-line (arg)
  "Kill ARG lines backward."
  (interactive "p")
  (kill-line (- 1 arg)))
#+end_src

=Command + Shift + Backspace=: Delete whole line entirely.\\
/This is not the default behavior of MacOS, but I found it's useful./
#+begin_src emacs-lisp
(global-set-key (kbd "s-S-<backspace>") 'kill-whole-line)
#+end_src


# TODO:
# 1. Comment on empty line, it adds (e.g.) and put the cursor behind
# 2. Comment one line, it adds before and forward one line
# 3. Comment on region, it add and move to the next line of the region
# 4. Cannot uncomment inside org mode code block
=Command + /=: Comment/Uncomment line(s).
#+begin_src emacs-lisp
(global-set-key (kbd "s-/") 'comment-or-uncomment-region-or-line)

(defun comment-or-uncomment-region-or-line ()
  "Comments or uncomments the region or the current line if
there's no active region."
  (interactive)
  (let (beg end)
    (if (region-active-p)
        (setq beg (region-beginning) end (region-end))
      (setq beg (line-beginning-position) end (line-end-position)))
    (comment-or-uncomment-region beg end)))
#+end_src

**** Behaviors
Auto pair brackets, quotes etc.
#+begin_src emacs-lisp
(electric-pair-mode 1)
#+end_src

Overwrite selection on pasting.
#+begin_src emacs-lisp
(delete-selection-mode 1)
#+end_src

Indent with 2 space.
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
(setq-default tab-width 2)
(setq indent-line-function 'insert-tab)
#+end_src

** Vim Emulator
[[https://github.com/emacs-evil/evil][Evil]] is an extensible vi layer for Emacs. It emulates the main features of Vim, and provides facilities for writing custom extensions.
#+begin_src emacs-lisp
(use-package evil
  :init
  (setq evil-want-keybinding nil)
  :config
  (evil-mode 1)
  :custom
  ; Use native keybindings on insert state.
  (evil-disable-insert-state-bindings t))
#+end_src

*** Behaviors
# TODO: Remove evil search highlight with ESC
Unbind =return= key in for using it to open link in org mode.
#+begin_src emacs-lisp
(with-eval-after-load 'evil-maps
    (define-key evil-motion-state-map (kbd "RET") nil))

(setq org-return-follows-link t)
#+end_src

Set Evil cursor color and styles in different situations.
You can try these commands independently to test the effects: =Esc, i, v, d, r=
#+begin_src emacs-lisp
(setq evil-normal-state-cursor '(box "#e2416c")
      evil-insert-state-cursor '(bar "#e2416c")
      evil-visual-state-cursor '(hollow "#e2416c")
      evil-operator-state-cursor '(evil-half-cursor "#e2416c")
      evil-replace-state-cursor '(hbar "#e2416c"))
#+end_src

Set default cursor style to bar when evil use emacs state.
#+begin_src emacs-lisp
(setq-default evil-emacs-state-cursor 'bar)
#+end_src

Records changes to separate undo instead of a big one in insert state.
#+begin_src emacs-lisp
(setq evil-want-fine-undo t)
#+end_src

*** Evil Collection
[[https://github.com/emacs-evil/evil-collection][evil-collection]], which provides evil-friendly bindings for many modes.
#+begin_src emacs-lisp
(use-package evil-collection
  :after evil
  :config
  (setq evil-collection-mode-list
        '(dired magit))
  (evil-collection-init))
#+end_src

*** Evil Surround
[[https://github.com/emacs-evil/evil-surround][evil-surround]] makes surround text with paired symbols easily.
#+begin_src emacs-lisp
(use-package evil-surround
  :after evil
  :config
  (global-evil-surround-mode 1))
#+end_src

*** Undo Fu
# Split and move to editing text
[[https://gitlab.com/ideasman42/emacs-undo-fu][Undo Fu]] is a simple, stable linear undo with redo.
#+begin_src emacs-lisp
(use-package undo-fu
  :config
  (define-key evil-normal-state-map "u" 'undo-fu-only-undo)
  (define-key evil-normal-state-map "\C-r" 'undo-fu-only-redo))

(global-unset-key (kbd "s-z"))
(global-set-key (kbd "s-z")   'undo-fu-only-undo)
(global-set-key (kbd "s-Z") 'undo-fu-only-redo)
#+end_src

[[https://gitlab.com/ideasman42/emacs-undo-fu-session][Undo fu session]] writes undo/redo information upon file save which is restored where possible when the file is loaded again.
#+begin_src emacs-lisp
(use-package undo-fu-session
  :config
  (setq undo-fu-session-incompatible-files '("/COMMIT_EDITMSG\\'" "/git-rebase-todo\\'")))

(global-undo-fu-session-mode)
#+end_src

** Multiple Cursor
https://github.com/hlissner/evil-multiedit
https://github.com/gabesoft/evil-mc
https://github.com/syl20bnr/evil-iedit-state
https://github.com/magnars/multiple-cursors.el

** Spell and Grammar
** Keybinding References
*Keybinding Values*:
| Meaning | Emacs Key Value | MacOS Key           |
|---------+-----------------+---------------------|
| Control | =C=               | =Control(Ctrl)=       |
| Meta    | =M=               | =Option(Alt)=         |
| Super   | =s=               | =Command=             |
| Shift   | =S=               | =Shift=               |
| -       | =s-z=             | =Command + Z=         |
| -       | =s-Z=             | =Command + Shift + Z= |

*Keybinding Functions*: [[https://www.masteringemacs.org/article/mastering-key-bindings-emacs][Reference]]
- =(define-key KEYMAP KEY DEF)=: Defines a key against a keyboard map. Use this if you want to change a keymap that isn‚Äôt the current buffer map.
- =(local-set-key KEY COMMAND)=: Binds a key to the local keymap used by the active buffer, unlike define-key which takes an explicit keymap to bind a key against.
- =(local-unset-key KEY)=: Removes KEY from the active, local keymap.
- =(global-set-key KEY COMMAND)=: Binds a key to the global keymap, making it available in all buffers (with a caveat ‚Äì see below.)
- =(global-unset-key KEY)=: Removes KEY from the global keymap

*Keybinding Value Styles*:
- =(kbd "s-Z")=
- ~"s-Z"~
- ~[s-Z]~

* Markup Language
** Org Mode
[[https://orgmode.org/][Org]] is a highly flexible structured plain text file format.
*** Behaviors
# 15. org render code block with hiding begin & end
# 17. org latex
# 18. Enable partial horizontal scroll in Emacs: https://github.com/misohena/phscroll
# 19. Smooth scrolling over images in Emacs
# 20. Preview equations live in org-mode
# 23. org roam
# Make not*Bold*AtAll work!
Enable headline and subcontent in the indented view.
#+begin_src emacs-lisp
(add-hook 'org-mode-hook 'org-indent-mode)
#+end_src

Fold all contents on opening a org file.
#+begin_src emacs-lisp
(setq org-startup-folded t)
#+end_src

Hide emphasis makers.
#+begin_src emacs-lisp
(setq org-hide-emphasis-markers t)
#+end_src

Enable shift selection.
#+begin_src emacs-lisp
(setq org-support-shift-select 'always)
#+end_src

Disable reindent on every time editing code block.
#+begin_src emacs-lisp
(setq org-edit-src-content-indentation 0)
#+end_src

Always display images.
#+begin_src emacs-lisp
(setq org-startup-with-inline-images t)
#+end_src

Do not display image actual width.
#+begin_src emacs-lisp
(setq org-image-actual-width nil)
#+end_src

*** Org Superstar
[[https://github.com/integral-dw/org-superstar-mode][Org Superstar]] prettifies headings and plain lists in Org mode.
#+begin_src emacs-lisp
(use-package org-superstar
  :config
  (add-hook 'org-mode-hook (lambda () (org-superstar-mode 1)))
  (setq org-hide-leading-stars t))
#+end_src

Change org headlines' style to ~‚Ä∫~.
# TODO: 
#+begin_src emacs-lisp
(setq org-superstar-headline-bullets-list '("‚Ä∫"))
#+end_src

Change org unordered list styles.
#+begin_src emacs-lisp
(setq org-superstar-prettify-item-bullets t)
(setq org-superstar-item-bullet-alist '((?* . ?‚Ä¢)
                                        (?+ . ?‚Ä¢)
                                        (?- . ?‚Ä¢)))
#+end_src

*** Org Appear
[[https://github.com/awth13/org-appear][Org Appear]] toggles visibility of hidden Org mode element parts upon entering and leaving an element.
#+begin_src emacs-lisp
(use-package org-appear)
(add-hook 'org-mode-hook 'org-appear-mode)
(setq org-appear-autolinks t)
#+end_src


** Markdown Mode
[[https://github.com/jrblevin/markdown-mode][Markdown]] allows you to write using an easy-to-read, easy-to-write plain text format.
#+begin_src emacs-lisp
(use-package markdown-mode
  :commands (markdown-mode gfm-mode)
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :init (setq markdown-command "multimarkdown"))
#+end_src

* Terminal Emulator
** Exec Path From Shell
[[https://github.com/purcell/exec-path-from-shell][exec-path-from-shell]] ensures environment variables inside Emacs look the same as in the user's shell.
#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :config
  (exec-path-from-shell-initialize))
#+end_src

** Vterm
# Open as a mini buffer
# Word wrap is not normal
# Text Editing keybindings are not supported
[[https://github.com/akermu/emacs-libvterm][Vterm]] is fully capable, fast, and it can seamlessly handle large outputs.
#+begin_src emacs-lisp
(use-package vterm)
#+end_src

*** Behaviors
Disable evil mode for vterm.
#+begin_src emacs-lisp
(add-hook 'vterm-mode-hook 'evil-emacs-state)
#+end_src

Close vterm buffer without confriming.
#+begin_src emacs-lisp
(add-hook 'vterm-mode-hook
          (lambda () (setq kill-buffer-query-functions nil)))
#+end_src

*** Keybindings
# TODO: Open, focus and hide vterm instance, create new instance.
#+begin_src emacs-lisp
(define-key vterm-mode-map (kbd "s-k") 'vterm-clear)
#+end_src

* Version Control
** Magit
[[https://github.com/magit/magit][Magit]] is an interface for [[https://git-scm.com/][Git]] inside Emacs.
#+begin_src emacs-lisp
(use-package magit
  :config
(setq magit-diff-refine-hunk t))
#+end_src

** Git Gutter
[[https://github.com/emacsorphanage/git-gutter][Git Gutter]] shows file changes in the left margin.
#+begin_src emacs-lisp
(use-package git-gutter
  :config
  (global-git-gutter-mode 't))
#+end_src

If you set ~git-gutter:update-interval~ seconds larger than 0, git-gutter updates diff information in real-time by idle timer.
#+begin_src emacs-lisp
(custom-set-variables '(git-gutter:update-interval 2))
#+end_src

* Chinese Optimization
** Font
Use macOS's default Chinese font for Chinese characters in Emacs.
#+begin_src emacs-lisp
(set-fontset-font "fontset-default" 'han '("STHeiti"))
#+end_src

** Display
Break lines normally for Chinese characters in visual line mode.
#+begin_src emacs-lisp
(setq word-wrap-by-category t)
#+end_src

# FIX: Line Spacing default with Chinese Characters like „ÄÇ

[[https://github.com/casouri/valign][valign]] can properly align tables containing variable-pitch font, CJK characters and images.
#+begin_src emacs-lisp
(use-package valign
  :config
  (add-hook 'org-mode-hook #'valign-mode)
  (setq valign-fancy-bar 1))
#+end_src

** Search
[[https://github.com/laishulu/evil-pinyin][evil-pinyin]]: Search Chinese characters with the first letter of Pinyin.
#+begin_src emacs-lisp
(use-package evil-pinyin
  :config
  (evil-select-search-module 'evil-search-module 'evil-search)
  (global-evil-pinyin-mode))
#+end_src

** Input Method
[[https://github.com/laishulu/emacs-smart-input-source][sis]] can auto switch to English input method and save the previous input method when entering Evil normal mode, restore the saved input method when switching back to Evil insert mode.
#+begin_src emacs-lisp
(use-package sis
  :config
  (sis-ism-lazyman-config
   "com.apple.keylayout.ABC"
   "com.apple.inputmethod.SCIM.ITABC")
  (sis-global-respect-mode t)
  (sis-global-context-mode t)
  (sis-global-inline-mode t))
#+end_src

** Keybindings
Make keybindings work under Chinese input method.

| Keybindings | Chinese Keybindings | Features              |
|-------------+---------------------+-----------------------|
| ~Command + [~ | ~Command + „Äë~        | Go to previous Buffer |
| ~Command + ]~ | ~Command + „Äê~        | Go to next Buffer     |

#+begin_src emacs-lisp
(global-set-key (kbd "s-„Äê") 'previous-buffer)
(global-set-key (kbd "s-„Äë") 'next-buffer)
#+end_src
